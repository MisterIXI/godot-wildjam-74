~ default
This is a test dialogue!
=> END

~ testbox
if !test_checked:
	This is a testbox!
	set test_checked = true
else:
	I've already checked it!
=> END

~ beginning
if !is_awake:
	Huh?
	Damn, I must've dozed off...
		...Where is everybody else?
	Is this the terminal station?
	set is_awake = true
=> END

~ outside
if !was_outside:
	Man, this place looks deserted...
	Is anyone even here?
	set was_outside = true
=> END

~ clock
if !clock_spinning and !clock_checked:
	It doesn't seem to work...
		In fact, it seems it hasn't been in working order for quite some time.
	set clock_checked = true
elif clock_checked and !clock_spinning:
	Still not working...
	 It's kinda accurate though. Time does seem to stand still in this place.
elif clock_spinning:
	What the fuck is going on here?
=> END

~ vending_machine
if !met_vending_machine:
	I have a few coins in my pocket, what do I want to get?
else:
	Maybe I should try again.
- Cola
	Alright the code for cola is 3712.
- Chips
	Yeah, chips sound good, The code is 4534.
- Grape Soda
	Grape Soda it is! The code is 1758.
- Bubble Gum
	I hadn't bubble gum for ages! The code is 0591.
- Nothing
	I've changed my mind. => END
#do vending_sfx.play()
if !met_vending_machine:
	The coin fell right through.
else:
	% Come on! Why?
	% Didn't work... again...
	% This machine sucks!
set met_vending_machine = true
=> END

~ ghost
if !ghost_met:
	Uhh, hello?
	Can you tell me where we are? When is this train is leaving the station?
	do creepy_ghost.play()
	do wait(3)
	Umm, okay?... It's okay, I'll look for somebody else.
	set ghost_met = true
else:
	... Nevermind.
=> END

~ clerk_cutscene
#if ghost_met and wrapped_around:
do GameManager.clerk_appear()
Oh, there's someone in there!
	I better talk to them! Maybe they know how I can get home.
set clerk_appear_happened = true
=> END

~ clerk
Hello, thank goodness, you are here!
	Can you tell me, when the next train is leaving?
	I dozed off in the train and missed my stop.
Clerk?: [speed=0.6]Well, let me see here.
	[speed=0.6]There is no train scheduled for the next [shake rate=20.0 level=5 connected=1]§&%$[/shake] hours.
Wait, what are you saying? There is no train coming?
Where do I leave this station then?
	I walked around but I couldn't find an exit...
Clerk?: [speed=0.6]Leave... Exit...?
[shake rate=20.0 level=5 connected=1]Maybe you should just sit down over there and wait. I’ll lock everything up here soon.[/shake]
What are you-
do GameManager.clerk_disappear()
do GameManager.unlock_counter_door()
do unlock_door.play()
The door unlocked... Maybe I should enter and ask him again?
=> END

~ inside_office
...Hello?
Where did he go?
=> END

~ calendar
It's an old calendar from 1992.
	The paper is yellowed and brittle.
It seems like it would crumble in my fingers if I touched it.
=> END

~ newspaper
if !newspaper_read:
	Here's a torn up newspaper article.
		...I can't make out much of it, but it seems to talk about this subway station.
[speed=0.6]...The city mayor decided to permanently close Glenwich Station and seal it off from the subway grid.
if !newspaper_read:
	What the hell? From when is this article? And how am I even here then?
I really need to get out of here!
set newspaper_read = true
=> END

~ office_cabinet
if !has_train_key:
	It's an old fashioned office cabinet, doesn't seem to be to that interesting.
	Wait! There is a key in one of the drawers.
	Let's see... it has a tag with a train symbol attached to it.
		I'll take it just in case.
	do key_rattle.play()
	set has_train_key = true
else:
	Just a regular old office cabinet. Reminds me of work...
=> END

~ office_exit
if has_train_key and newspaper_read:
	I'll better get out of here...
		This place is giving me the creeps!
=> END

~ train_door
if has_train_key and !train_key_broken:
	Yes! The key fits!
	do unlock_door.play()
	#do key_break_sfx.play()
	do environment.train.set_door_inside(true)
	set train_key_broken = true
	Fuck, it broke off!
	Well, at least the door is open...
elif has_train_key and train_key_broken:
	do locked_door.play()
	Damn it! It doesn't move an inch and the broken key is stuck.
else:
	do locked_door.play()
	It's locked.
=> END

~ ghosts
% Um, hello?
% Do you speak english?
% Excuse me, can you tell me-
do creepy_ghost.play()
% Uh, right...
% ...
% Okay...
=> END

~ train_door_close
do player.turn(Vector2.RIGHT)
Oh, no! This is bad...
=> END

~ train_operator_door
set ghost_turn = true
Ghost: [speed=0.5][shake rate=20.0 level=5 connected=1]...L..E..A..V..E...[/shake]
# blackout with teleport to start pos
do GameManager.train_blackout()
Huh?
Damn, I must've dozed off...
	...Where is everybody else?
Is this the terminal sta...
Wait,[wait=0.5] did I black out?
	This has already happened...
set act_2 = true
=> END

~ train_leave
# only do this is act 2 is active
#do game_manager.train_door_close_outside()
What?
#do game_manager.train_leave()
set train_left = true
Wait!! [wait=1]Don't leave me here!
Where did they go?
Was this wall here before?
=> END

~ black_door
Okay, this ominous looking hole was definitely not here before,[wait=0.5] right?
Man, I think I'm losing my mind...
=> END


~ toilet_door
if !has_toilette_key:
	It's the door to the toilet.
	do locked_door.play()
	Damn, it's locked.
else:
	do unlock_door.play()
	Well, here we go...
	do GameManager.remove_toilette_door()
	set toilette_key_used = true
=> END

~ janitor_closet
This seems to be a janitor's closet.
Maybe there is something of use here?
=> END

~ janitor_shelf
They store here cleaning utensils and tools.
It's mostly old dusty stuff though.
=> END

~ padlocked_safe
if !safe_open:
	It's a small safe with a padlock.
		There's a note attached to it.
	What should I do?
	- Read the note
		"Hey Frank, next time you forget the combination to the key safe, you can buy me a grape soda. Maybe that’ll jog your memory. -Dave"
	- Check the padlock
		It's a four digit padlock.
		do GameManager.main.padlock.dialogue_trigger()
		if current_padlock_code == padlock_code:
			It's open!
			set safe_open = true
		else:
			That wasn't it...
	- Do nothing
else:
	if !has_toilette_key:
		Ok, let's see...
			Here's another key. It seems to be for the toilets.
		do key_rattle.play()
		set has_toilette_key = true
		Okay, now let's check the toilets.
	else:
		Now it's empty.
=> END

~ janitor_note
It's a note to the staff asking to gather any personal belongings before the closing date.
They really were in a hurry to close this station, huh?
=> END

~ inside_toilet
...It's a regular old toilet.
	And it stinks like death in here.
do wait(0.5)
Man, what am I even doing here? [wait=0.5] This whole situation is completely fucked.
do player.turn(Vector2.DOWN)
do wait(1)
do player.turn(Vector2.UP)
I can't find the exit and there's noone here. What should I do?
do player.turn(Vector2.RIGHT)
...[wait=1]What's this?
do player.move_to(environment.newspaper_marker.global_position)
...[wait=1]It's another newspaper clipping.
"49 dead after freak accident in Glenwich Station
	Yesterday a part of Glenwich Station collapsed and got completely blocked by debris trapping all 44 passengers and 5 members of the staff.
The situation worsened further when a broken pipe of the city's freshwater system quickly filled the station with water.
After the several hours lasting rescue attempts of the firefighters and police officers, all victims of this tragedy were pronounced dead on the scene."
This article is from October 1992...
do creepy_sound.play()
do player.turn(Vector2.LEFT)
do wait(1.5)
Oh no, what is happening now?
	I need to hide quickly!
do environment.hallway.set_toilette_door(false)
=> END

~ left_stall
It's closed!
=> END

~ toilet_stall
do environment.hallway.kill_toilette_interactionbox()
do environment.hallway.set_stall_door(true)
do wait(1)
do player.move_to(Vector2(1520,-665))
do player.wait_for_player()
do player.turn(Vector2.DOWN)
do environment.hallway.set_stall_door(false)

do environment.hallway.toilette_ghost_animation()

do environment.hallway.set_stall_door(true)
do environment.hallway.set_toilette_door(true)
do player.move_to(Vector2(1520,-531))
do player.turn(Vector2.DOWN)
do player.wait_for_player()
do environment.hallway.set_hallway_extension(true)
=> END

~ dead_end
It's a dead end...
    What now?
do environment.hallway.ghost_appear()
do player.turn(Vector2(-1,0))
Oh my goodness! Please leave me alone!
do environment.hallway.ghost_approach_player()
Please! Nooo...
do environment.hallway.ghost_disappear()
...?
set has_ticked = true
do environment.train.reset_position()
Th- they gave me a ticket?
do environment.hallway.switch_hall(true)
do environment.walls.set_wall_right(true)
=> END

~ train_arrival
It's the train!!
	Please just leave this place already!
=> END

~ cutscene_tester

Gott sei dank der Zug ist noch da, da kann ich noch eine rauchen.
do player.start_smoking()
do wait(5)
do environment.train.depart()
do wait(0.5)
do player.move_to(Vector2(-3500,0), 350)
do environment.walls.set_wall_left(true)
do player.wait_for_player()
FUUUUUCK!
=> END

