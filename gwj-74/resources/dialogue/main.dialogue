~ default
This is a test dialogue!
=> END

~ testbox
if !test_checked:
	This is a testbox!
	set test_checked = true
else:
	I've already checked it!
=> END

~ beginning
Huh?
Damn, I must've dozed off...
	...Where is everybody else?
Is this the terminal station?
=> END

~ outside
if !was_outside:
	Man, this place looks deserted...
	Is anyone even here?
	set was_outside = true
=> END

~ clock
if !clock_spinning and !clock_checked:
	It doesn't seem to work...
		In fact, it seems it hasn't been in working order for quite some time.
	set clock_checked = true
elif clock_checked and !clock_spinning:
	Still not working...
	 It's kinda accurate though. Time does seem to stand still in this place.
elif clock_spinning:
	What the fuck is going on here?
=> END

~ vending_machine
if !met_vending_machine:
	I have a few coins in my pocket, what do I want to get?
else:
	Maybe I should try again.
- Cola
	Alright the code for cola is 3712.
- Chips
	Yeah, chips sound good, The code is 4534.
- Grape Soda
	Grape Soda it is! The code is 1758.
- Bubble Gum
	I hadn't bubble gum for ages! The code is 0591.
- Nothing
	I've changed my mind. => END
#do vending_sfx.play()
if !met_vending_machine:
	The coin fell right through.
else:
	% Come on! Why?
	% Didn't work... again...
	% This machine sucks!
set met_vending_machine = true
=> END

~ ghost
if !ghost_met:
	Uhh, hello?
	Can you tell me where we are? When this train is leaving the station?
	#do ghost_sfx.play()
	Umm, okay?... It's okay, I'll look for somebody else.
	set ghost_met = true
=> END

~ clerk_cutscene
if ghost_met and wrapped_around and clerk_appear_happened:
	#do game_manager.clerk_appear()
	Oh, there's someone in there!
		I better talk to them! Maybe they know how I can get home.
	set clerk_appear_happened = true
=> END

~ clerk
Hello, thank goodness, you are here!
	Can you tell me, when the next train is leaving?
	I dozed off in the train and missed my stop.
Clerk?: [speed=0.6]Well, let me see here.
	[speed=0.6]There is no train scheduled for the next [shake rate=20.0 level=5 connected=1]§&%$[/shake] hours.
Wait, what are you saying? There is no train coming?
Where do I leave this station then?
	I walked around but I couldn't find an exit...
Clerk?: [speed=0.6]Leave... Exit...?
[shake rate=20.0 level=5 connected=1]Maybe you should just sit down over there and wait. I’ll lock everything up here soon.[/shake]
What are you-
#do game_manager.clerk_disappear()
#do unlock_sfx.play()
The door unlocked... Maybe I should enter and ask him again?
=> END

~ inside_office
...Hello?
Where did he go?
#do lightswitch_sfx.play()
The lightswitch doesn't work...
=> END

~ calendar
It's an old calendar from 1992.
	The paper is yellowed and brittle.
It seems like it would crumble in my fingers if I touched it.
=> END

~ newspaper
if !newspaper_read:
	Here's a torn up newspaper article.
		...I can't make out much of it, but it seems to talk about this subway station.
[speed=0.6]...The city mayor decided to permanently close Glenwich Station and seal it off from the subway grid.
if !newspaper_read:
	What the hell? From when is this article? And how am I even here then?
I really need to get out of here!
set newspaper_read = true
=> END

~ key_hanger
if !has_train_key:
	It's a key hanger, but there is only one key here.
	Let's see... it has a tag with a train symbol attached to it.
		I'll take it just in case.
	set has_train_key = true
else:
	Just a regular old key hanger. There are no keys left.
=> END

~ office_exit
if has_train_key and newspaper_read:
	I'll better get out of here...
		This place is giving me the creeps!
=> END

~ train_door
if has_train_key and !train_key_broken:
	Yes! The key fits!
	#do unlock_sfx.play()
	#do key_break_sfx.play()
	do environment.train.set_door_inside(true)
	set train_key_broken = true
	Fuck, it broke off!
	Well, at least the door is open...
elif has_train_key and train_key_broken:
	#do locked_sfx.play()
	Damn it! It doesn't move an inch and the broken key is stuck.
else:
	#do locked_sfx.play()
	It's locked.
=> END

~ ghosts
% Um, hello?
% Do you speak english?
% Excuse me, can you tell me-
#do ghost_sfx.play()
% Uh, right...
% ...
% Okay...
=> END


~ cutscene_tester

Gott sei dank der Zug ist noch da, da kann ich noch eine rauchen.
do player.start_smoking()
do wait(5)
Heyy!!!!!!
do player.move_to(Vector2(-1500,0), 300)
#do player.wait_for_player()
do environment.train.depart()
do player.wait_for_player()
FUUUUUCK!
=> END